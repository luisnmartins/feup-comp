options {
	MULTI = true;
	DEBUG_PARSER = false;
	STATIC = true;
	JJTREE_OUTPUT_DIRECTORY = "parser";
	OUTPUT_DIRECTORY = "parser";
}

PARSER_BEGIN(Parser)
	package parser;
	import java.io.*;

	

	public class Parser{
		static Integer counter = 0;
	}


PARSER_END(Parser)

SKIP : {
	" "
	| "\t"
	| "\n"
	| "\r"
	| <"//" (~["\n","\r"])* ("\n" | "\r" | "\r\n")>
	| <"/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/">
}

/* reserved words */
TOKEN[IGNORE_CASE] : {
<RELA_OP: ">" | "<" | "<=" | ">=" | "==" | "!=">
| <ADDSUB_OP: "+" | "-">
| <ARITH_OP: "*" | "/" | "<<" | ">>" | ">>>">
| <BITWISE_OP: "&" | "|" | "^">
| <NOT_OP: "!">
| <WHILE: "while">
| <IF: "if">
| <ELSE: "else">
| <ASSIGN: "=">
| <ASPA: "\"">
| <LPAR: "(">
| <RPAR: ")">
| <VIRG: ",">
| <PVIRG: ";">
| <LCHAVETA: "{">
| <RCHAVETA: "}">
| <FUNCTION: "function">
| <MODULE: "module">
| <SIZE: "size">
}

TOKEN : {
<INTEGER: (<DIGIT>)+>
| <ID: <LETTER> (<LETTER> | <DIGIT>)*>
| <#LETTER: ["$","A"-"Z","_","a"-"z"]>
| <#DIGIT: ["0"-"9"]>
| <STRING: "\"" (["a"-"z","A"-"Z","0"-"9",":"," ","="])+ "\"">
}


TOKEN_MGR_DECLS : {
	Boolean error = false;
}

//Function used to skip tokens until kind. This returns the last token that is kind
JAVACODE
Token error_skipto(int kind, ParseException e) {
	Token t = null;
	if(counter < 10){
	System.out.println(e.toString());
	counter++;
	
	do{
		t=getNextToken();
	}while(t.kind != kind);
}
	return t;
}

//Function used to skip tokens until kind or kind2. This returns the last token that is kind or kind2
JAVACODE 
Token error_skipto2(int kind, int kind2, ParseException e) { 
	Token t = null;
	if(counter < 10){

	
	counter++;
  System.out.println(e.toString()); 
  do{ 
     
	t=getNextToken(); 
	
  }while(t.kind != kind && t.kind != kind2); 

}

  return t;
}

SimpleNode Module(): {Token t;}  
{
	try {
		<MODULE> t=<ID> {jjtThis.name = t.image;} <LCHAVETA> (Declaration())* (
			Function())* <RCHAVETA> 
	} catch (ParseException e) {
		{token_source.error = true;}
		error_skipto(RCHAVETA, e);
		
	} 
	{if(token_source.error) return null; else return jjtThis;}
}

void Declaration() : {Token t,v, k;}
{
	Element()
	( (k=<ASSIGN> {jjtThis.line = k.beginLine;})  (("[" ArraySize() "]") | 
	((t=<ADDSUB_OP> {jjtThis.signal = t.image;})? (v=<INTEGER>){jjtThis.value=Integer.parseInt(v.image);})#DeclarationValue))? <PVIRG>
}

void Function() : {Token t = null, v = null;}
{
	
	try{
	 <FUNCTION> (t=<ID>{jjtThis.line = t.beginLine;})
	}catch(ParseException e){
		{token_source.error = true;}
		t = error_skipto(ID, e);
		
	}
	try{
	( ( (ArrayElement(){jjtThis.returnArray = true;})? <ASSIGN> (v=<ID>) <LPAR> (Varlist())? <RPAR> ) | (<LPAR> ( Varlist() )? <RPAR> ) )
		
	}catch(ParseException e){
		{token_source.error = true;}
		error_skipto(RPAR, e);
		
	}
	
	try{
		<LCHAVETA> Stmtlst() <RCHAVETA>
	}catch(ParseException e){
		{token_source.error = true;}
		error_skipto(RCHAVETA, e);
		
	}
	
 	{
		if(v == null) {
			jjtThis.name = t.image;
		} else {
			jjtThis.returnValue = t.image;
			jjtThis.name = v.image;
		}
	 }
}

void Element() :{Token t;}
{
	(t=<ID> {jjtThis.name = t.image;jjtThis.line = t.beginLine;}) ((ArrayElement() {jjtThis.isArray = true;}))?
}

void Varlist() : {}
{
	Element() ( <VIRG> Element())*
}

void ArrayElement() #void : {}
{
	"[" "]"
}


void Stmtlst() : {}
{
	 ( Stmt() )*
	 
}

void Stmt() #void : {}
{
	
	While() 
	| 
	If() 
	| 
	try{	
		(( (LOOKAHEAD(3) Call() | Assign() )) <PVIRG>)
	}catch(ParseException e){
		{token_source.error = true;}
		Token bracket = error_skipto2(PVIRG, LCHAVETA, e);
		if(bracket.kind == LCHAVETA)
			Phantom();
		
	}	
}

void Phantom() : {}
{
	
	Stmtlst() <RCHAVETA>
	
}


void Call() : {Token t,v;}
{
	t=<ID>{jjtThis.name = t.image; jjtThis.line = t.beginLine;} ( "." v=<ID>{jjtThis.isCaller = true; jjtThis.functionCalled = v.image;} )? 
	<LPAR> ( ArgumentList() )? <RPAR>
	
}


void Assign() : {Token t;}
{
	Lhs() t=<ASSIGN>{jjtThis.line = t.beginLine;} Rhs()
}

void Lhs() #void : {}
{
	Access()

}

void Rhs() : {Token t;}
{
	( Term() ( ( (t=<ARITH_OP> {jjtThis.operator=t.image;jjtThis.line = t.beginLine;}) | 
	(t=<BITWISE_OP> {jjtThis.operator = t.image;jjtThis.line = t.beginLine;}) | 
	(t=<ADDSUB_OP> {jjtThis.operator = t.image;jjtThis.line = t.beginLine;}) ) Term() )? )
		| "[" ArraySize() "]"

}


void ArraySize() : {Token t;}
{
	((t=<ID> {jjtThis.isSize = Boolean.valueOf(ScalarAccess()); jjtThis.sizeVar = t.image; jjtThis.line = t.beginLine;}) | 
	(t = <INTEGER> {jjtThis.size = Integer.parseInt(t.image);}))
}

void Term()  : {Token t,v;}
{
	( ( t=<ADDSUB_OP> {jjtThis.operation = t.image;} )? 
	( (v=<INTEGER>{jjtThis.value = Integer.parseInt(v.image);}) | LOOKAHEAD(3) Call() | Access()) ) 
}

void Exprtest()  : {Token t;}
{
	
	try{
	<LPAR> Lhs() (t=<RELA_OP> {jjtThis.operator = t.image;jjtThis.line = t.beginLine;})Rhs() <RPAR>
	}catch(ParseException e){
		{token_source.error = true;}
		error_skipto(RPAR, e);
		
	}
}

void While() : {}
{
	<WHILE>
	try{
	Exprtest() <LCHAVETA>
	}catch(ParseException e){
		{token_source.error = true;}
		error_skipto(LCHAVETA, e);
	}
	
	try{
	 Stmtlst() <RCHAVETA>
	}catch(ParseException e){
		{token_source.error = true;}
		error_skipto(RCHAVETA, e);
	}
}

void If() : {}
{
	try{
		<IF> Exprtest() <LCHAVETA> Stmtlst() <RCHAVETA> 
	}catch(ParseException e){
		{token_source.error = true;}
		error_skipto(RCHAVETA, e);
	}
	try{
		( (<ELSE> <LCHAVETA> Stmtlst() <RCHAVETA>) #Else )?
	}catch(ParseException e){
		{token_source.error = true;}
		error_skipto(PVIRG, e);
	}
	
}

void ArgumentList() #void : {}
{
	
	Argument() ( <VIRG> Argument() )*
	
	
}

void Argument() : {Token t;}
{
	( (t=<ID>{jjtThis.value = t.image; jjtThis.isID = true;}) | 
	(t=<STRING>{jjtThis.value = t.image;}) | 
	(t=<INTEGER>{jjtThis.value = t.image;}) )
}

void Access() : {Token t;}{
	(t=<ID>{jjtThis.name = t.image; jjtThis.line = t.beginLine;}  (ArrayAccess()| {jjtThis.isSize = Boolean.valueOf(ScalarAccess());}))
}

void ArrayAccess() #void: {}
{
	"[" Index() "]"
}

boolean ScalarAccess() #void: {}
{
	(("." <SIZE> {return true;}) |({} {return false;}))
}

void Index() : {Token t;}
{
	((t=<ID> {jjtThis.value = t.image; jjtThis.line = t.beginLine;}) | (t=<INTEGER> {jjtThis.isInt = true; jjtThis.value = t.image;}))
}
